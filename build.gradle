plugins {
    id 'java'
}

group = 'com.template'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    useJUnitPlatform()

    // Enable detailed test logging
    testLogging {
        events "passed", "skipped", "failed"
        showExceptions true
        showStackTraces true
        showStandardStreams true
    }

    // Define report outputs
    reports {
        html.required.set(true) // HTML report
        junitXml.required.set(true) // XML report
    }

    doLast {
        def testPath = "${project.getProjectDir()}/build/test-results/test/TEST-com.template.AppTest.xml"
        println "Test report file: ${testPath}"
        def testReportFile = file(testPath)
        if (testReportFile.exists()) {
            def xmlParser = new XmlParser()
            def testSuite = xmlParser.parse(testReportFile)
            def testCount = testSuite.'@tests'.toInteger()
            if (testCount < 10) {
                throw new GradleException("At least 10 tests are required. Only ${testCount} tests were found.")
            }
        } else {
            throw new GradleException("Test report file not found.")
        }
    }
}
